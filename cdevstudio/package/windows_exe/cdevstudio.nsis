; ===== Defines =====
!define APPLICATION_VERSION "0.0.8"
!define APPLICATION_NAME "CDevStudio"
!define GENERIC_APPLICATION_NAME "cdevstudio"
!define WORKSPACE_NAME "cdevstudio-workspace"
!define PROJECT_ROOT "..\.."
;!define QT_ROOT_DIR "C:\Qt\Qt5.3.1\5.3\msvc2012_opengl"
!define QT_BINARY_DIR "${QT_ROOT_DIR}\bin"
!define QT_PLUGINS_DIR "${QT_ROOT_DIR}\plugins"

; ===== Qt check =====
!ifndef QT_ROOT_DIR
	!error "QT_ROOT_DIR is not defined. You have to define it in the cdevstudio.nsis file!"
!endif

; ===== Includes =====
!include "MUI2.nsh"

; ===== General =====
Name "${APPLICATION_NAME} ${APPLICATION_VERSION}"
BrandingText "${APPLICATION_NAME} ${APPLICATION_VERSION}"
OutFile "${GENERIC_APPLICATION_NAME}-${APPLICATION_VERSION}-installer.exe"
InstallDir "$PROGRAMFILES\${WORKSPACE_NAME}"
SetCompressor "lzma"

; ===== Pages =====
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "..\..\LICENSE.md"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_CHECKED
!define MUI_FINISHPAGE_RUN_TEXT "Run Application"
!define MUI_FINISHPAGE_RUN_FUNCTION "RunApplication"
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

; ===== Installer =====
Section ""
	SetOutPath $INSTDIR
	
	File ${PROJECT_ROOT}\LICENSE.md
	
	!ifdef MINGW-MAKE
		File ${PROJECT_ROOT}\build\output\cdevstudio.exe
		File ${PROJECT_ROOT}\build\output\libcdevstudioplatform.dll
		File ${PROJECT_ROOT}\build\output\libpluginproject.dll
		File ${PROJECT_ROOT}\build\output\libpluginprojectexplorer.dll
		File ${PROJECT_ROOT}\build\output\libplugineditor.dll
		File ${QT_BINARY_DIR}\libgcc_s_dw2-1.dll
		File ${QT_BINARY_DIR}\libstdc++-6.dll
		File ${QT_BINARY_DIR}\libwinpthread-1.dll
	!else ifdef MSVC-MSBUILD
		File ${PROJECT_ROOT}\build\output\Release\cdevstudio.exe
		File ${PROJECT_ROOT}\build\output\Release\cdevstudioplatform.dll
		File ${PROJECT_ROOT}\build\output\Release\pluginproject.dll
		File ${PROJECT_ROOT}\build\output\Release\pluginprojectexplorer.dll
		File ${PROJECT_ROOT}\build\output\Release\plugineditor.dll
	!else
		!error "The compiler platform is not defined. Please run the batch file for your compiler platform"
	!endif
	
	File ${QT_BINARY_DIR}\Qt5Widgets.dll
	File ${QT_BINARY_DIR}\Qt5Gui.dll
	File ${QT_BINARY_DIR}\Qt5Core.dll
	File ${QT_BINARY_DIR}\icudt52.dll
	File ${QT_BINARY_DIR}\icuin52.dll
	File ${QT_BINARY_DIR}\icuuc52.dll
	
	CreateDirectory $INSTDIR\platforms
	SetOutPath $INSTDIR\platforms
	File ${QT_PLUGINS_DIR}\platforms\qminimal.dll
	File ${QT_PLUGINS_DIR}\platforms\qwindows.dll
	
	SetOutPath $INSTDIR
	WriteUninstaller $INSTDIR\uninstall.exe
SectionEnd

; ===== Uninstaller =====
Section "Uninstall"
	Delete $INSTDIR\*.exe
	Delete $INSTDIR\*.dll
	Delete $INSTDIR\platforms\*.dll
	Delete $INSTDIR\LICENSE.md
	Delete $INSTDIR\uninstall.exe
	RMDir $INSTDIR\platforms
	RMDir $INSTDIR
SectionEnd

; ===== Functions =====
Function RunApplication
	ExecShell "" "$INSTDIR\cdevstudio.exe"
FunctionEnd
